function PlayerStateFree() {
	//Movement
	xSpeed = lengthdir_x(inputMagnitude * speedWalk, inputDirection);
	ySpeed = lengthdir_y(inputMagnitude * speedWalk, inputDirection);

	PlayerCollision();

	//Update Sprite Index
	var _oldSprite = sprite_index;

	if (inputMagnitude != 0)
	{
		direction = inputDirection;
		sprite_index = spriteRun;
	}
	else
	{
		sprite_index = spriteIdle;
	}

	if (_oldSprite != sprite_index)
	{
		localFrame = 0;
	}

	//Update Image Index
	PlayerAnimateSprite();

	if (global.iLifted == noone)
	{
		//Attack key logic
		if (keyAttack)
		{
			state = PlayerStateAttack;
			stateAttack = AttackSlash;
		}
	}
	

	//Activate key logic
	if (keyActivate)
	{
		//1. Check for an entity to activate
		//2. If nothing, or smth with no script
			//2a. If carrying smth, Throw
			//2b. If not, Roll
		//3. Otherwise, activate entity
		//4. If entity is NPC, make it face towards player
	
		var _activateX = x + lengthdir_x(10, direction);
		var _activateY = y + lengthdir_y(10, direction);
		var _activateSize = 4;
		var _activateList = ds_list_create();
		activate = noone;
		var _entitiesFound = collision_rectangle_list(
			_activateX - _activateSize,
			_activateY - _activateSize,
			_activateX + _activateSize,
			_activateY + _activateSize,
			parentEntity,
			false,
			true,
			_activateList,
			true
		);
	
		//if the first instance found is lifted or has no script, try the next one
		while (_entitiesFound > 0)
		{
			var _check = _activateList[| --_entitiesFound];
			if (_check != global.iLifted) && (_check.entityActivateScript != -1)
			{
				activate = _check;
				break;
			}
		}
	
		ds_list_destroy(_activateList);
	
		if (activate == noone)
		{
			//Throw smth if held
			if (global.iLifted != noone)
			{
				PlayerThrow();
			}
			else
			{
				state = PlayerStateRoll;
				moveDistanceRemaining = distanceRoll;
			}
		}
		else
		{
			if (!activate.entityConsumable) && (!object_is_ancestor(activate.object_index, parentEnemy))
			{
				ScriptExecuteArray(activate.entityActivateScript, activate.entityActivateArgs);
		
				if (instance_exists(activate))
				{	
					//Make an NPC face the player
					if (activate.entityNPC)
					{
						with (activate)
						{
							direction = point_direction(x, y, objPlayer.x, objPlayer.y);
							image_index = CARDINAL_DIR;
						}
					}
				}
			}
		}
	}


}
